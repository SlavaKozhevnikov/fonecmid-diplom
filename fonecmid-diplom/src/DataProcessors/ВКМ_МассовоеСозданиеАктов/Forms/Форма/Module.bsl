
#Область ОбработчикиСобытийЭлементовШапкиФормы 
 
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ЗаполнениеТаблицыДоговоровИРеализаций();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	ДлительнаяОперация = СоздатьДокументыНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ОповещениеОПрогрессе = Новый ОписаниеОповещения("ОповещениеОПрогрессе", ЭтотОбъект); 
		
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = 	ОповещениеОПрогрессе; 
    
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект); 
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	    		
КонецПроцедуры   

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура ЗаполнениеТаблицыДоговоровИРеализаций()  
	
	Объект.ДоговорыИРеализации.Очистить();
	
	Объект.ДоговорыИРеализации.Очистить();
	
	Запрос = Новый Запрос;          
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ВТ_Реализации.Ссылка КАК Реализация,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент
	|ИЗ
	|	ВТ_Реализации КАК ВТ_Реализации
	|		ПРАВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО (ДоговорыКонтрагентов.Ссылка = ВТ_Реализации.Ссылка.Договор)
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
	|	И ДоговорыКонтрагентов.ВКМ_ПериодДействияС >= &ДатаНачала
	|	И ДоговорыКонтрагентов.ВКМ_ПериодДействияПо <= &ДатаОкончания";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	Запрос.УстановитьПараметр("ДатаНачала", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.Период.ДатаОкончания);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.ДоговорыИРеализации.Добавить();
		НоваяСтрока.Договор = Выборка.Договор;
		НоваяСтрока.Реализация = Выборка.Реализация; 
	КонецЦикла;
	  	
КонецПроцедуры 

&НаСервере
Функция СоздатьДокументыНаСервере() 
	
	Договоры = Новый Массив;
		
	Для Каждого Строка Из Объект.ДоговорыИРеализации Цикл
		Если Строка.Реализация = Документы.РеализацияТоваровУслуг.ПустаяСсылка() Тогда
			ДанныеСтроки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Строка.Договор, "Ссылка, Организация, Владелец");
			Договор = Новый Структура;
			Договор.Вставить("Организация", ДанныеСтроки.Организация);
			Договор.Вставить("Контрагент", ДанныеСтроки.Владелец);
			Договор.Вставить("Ссылка", ДанныеСтроки.Ссылка);
			Договоры.Добавить(Договор);			
		КонецЕсли;
	КонецЦикла;
	
	Дата = Объект.Период.ДатаОкончания;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, 
	"Обработки.ВКМ_МассовоеСозданиеАктов.МассовоеСозданиеРеализацийУслуг", Договоры, Дата);
	
	Возврат ДлительнаяОперация;

КонецФункции 

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		ЗаполнениеТаблицыДоговоровИРеализаций(); 
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПолучитьИзВременногоХранилища(Результат.АдресРезультата)); 
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОПрогрессе(Прогресс, ДополнительныеПараметры) Экспорт
	
	Если Прогресс.Прогресс <> Неопределено Тогда
		Состояние("Выполняется заполнение документов", Прогресс.Прогресс.Процент);
	КонецЕсли;
 			
КонецПроцедуры 

#КонецОбласти



